apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Iterate official Android SDK'
                    url = 'https://github.com/iteratehq/iterate-android'
                    licenses {
                        license {
                            name = 'Iterate License'
                            url = 'https://github.com/iteratehq/iterate-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'msingleton'
                            name = 'Mike Singleton'
                            email = 'mike@iteratehq.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/iteratehq/iterate-android.git'
                        developerConnection = 'scm:git:ssh://github.com/iteratehq/iterate-android.git'
                        url = 'https://github.com/iteratehq/iterate-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
